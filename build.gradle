plugins {
  id "us.ihmc.gradle.ihmc-build" version "0.0.19"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
version = '0.1'

project.ext.fullVersion = version
project.ext.vcsUrl = "https://github.com/ihmcrobotics/ihmc-geometry-basics"
project.ext.licenseURL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
project.ext.licenseName = "Apache License, Version 2.0"
project.ext.bintrayLicenseName = "Apache-2.0"

jar {
    manifest {
        attributes(
                "Created-By": "IHMC Gradle Build Script",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "IHMC",

                "Bundle-Name": project.name,
                "Bundle-Version": project.version,
                "Bundle-License": "IHMC Proprietary",
                "Bundle-Vendor": "IHMC")
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
   classifier = 'sources'
   from sourceSets.main.allJava
}

publishing {
   publications {
      mavenJava(MavenPublication) {
         groupId "us.ihmc"
         artifactId project.name
         version "$version"
         from components.java
		 
		 pom.withXml {
             asNode().children().last() + {
                 resolveStrategy = Closure.DELEGATE_FIRST
                 name project.name
                 description 'IHMC Geometry Basics is library providing the common tools useful for geometric applications.'
                 url 'https://github.com/ihmcrobotics/ihmc-geometry-basics'
                 licenses {
                     license {
                         name 'The Apache Software License, Version 2.0'
                         url 'http://www.apache.org/license/LICENSE-2.0.txt'
                         distribution 'repo'
                     }
                 }
                 developers {
                     developer {
                         id 'sbertrand'
                         name 'Sylvain Bertrand'
                         email 'sbertrand@ihmc.us'
                     }
                     developer {
                         id 'dcalvert'
                         name 'Duncan Calvert'
                         email 'dcalvert@ihmc.us'
                     }
                 }
             }
         }

		artifact sourceJar {
			classifier "sources"
		}
      }
   }
}

def labels = ['ihmc', 'geometry', 'java']
ihmc.configureForIHMCOpenSourceBintrayPublish(false, "mavenJava", "maven-release", labels)

repositories ihmc.ihmcDefaultArtifactProxies()

repositories {
   mavenLocal()
   jcenter()
   mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.pitest', name: 'pitest', version: '1.1.9'
    testCompile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
    testCompile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'

    compile group: 'org.ejml', name: 'dense64', version: '0.30'
}
